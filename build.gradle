import org.apache.tools.ant.filters.EscapeUnicode

buildscript {
    ext.kotlinVersion = kotlinVersion
    repositories {
        maven { url = 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.3.4'
}

repositories {
    maven { url = 'http://maven.aliyun.com/nexus/content/groups/public/' }
    jcenter()
    mavenCentral()
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

group = 'cn.yiiguxing.plugin.translate'
version = "${version}${buildNumber ? '-' + buildNumber : ''}"

intellij {
    version = ideaVersion
    pluginName = 'TranslationPlugin'
    downloadSources = Boolean.valueOf(sources)
}

patchPluginXml {
    sinceBuild customSinceBuild
    untilBuild customUntilBuild
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile('com.googlecode.soundlibs:mp3spi:1.9.5.4') {
        exclude module: 'junit'
    }
}

sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

[compileKotlin, compileTestKotlin]*.kotlinOptions {
    languageVersion = kotlinLanguageVersion
    apiVersion = kotlinTargetVersion
    jvmTarget = javaTargetVersion
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources) {
    filesMatching('**/*.properties') {
        filter EscapeUnicode
    }
}
