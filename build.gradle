import org.apache.tools.ant.filters.EscapeUnicode

buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
        mavenCentral()
    }
}

plugins {
    // https://github.com/JetBrains/gradle-intellij-plugin
    id "org.jetbrains.intellij" version "0.7.3"
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

repositories {
    maven { url "https://maven.aliyun.com/repository/public" }
    mavenCentral()
}

group = 'cn.yiiguxing.plugin.translate'
version = "${version}${buildNumber ? '-' + buildNumber : ''}"

intellij {
    version = ideaVersion
    pluginName = 'TranslationPlugin'
    downloadSources = Boolean.valueOf(sources)
    sandboxDirectory 'sandbox'
    plugins "java", "org.jetbrains.kotlin", "Pythonid:193.5233.12", "Dart:193.5233.18", "org.jetbrains.plugins.go:193.5233.48.65"
}

patchPluginXml {
    sinceBuild customSinceBuild
    untilBuild customUntilBuild
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'org.jsoup:jsoup:1.14.2'
    implementation 'org.apache.commons:commons-dbcp2:2.8.0'
    implementation 'commons-dbutils:commons-dbutils:1.7'
    implementation('com.googlecode.soundlibs:mp3spi:1.9.5.4') {
        exclude module: 'junit'
    }
}

sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

compileKotlin {
    kotlinOptions {
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinTargetVersion
        jvmTarget = javaTargetVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinTargetVersion
        jvmTarget = javaTargetVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources) {
    filesMatching('**/*.properties') {
        filter EscapeUnicode
    }
}
